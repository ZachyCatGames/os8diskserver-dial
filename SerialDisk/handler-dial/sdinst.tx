*20
/LAP6-DIAL-MS SERIAL DISK HANDLER INSTALLER
/BY ZACHARY KIRTZ
/
/DEFINITIONS
MTADR=3300	/MTABLE ADDR IN DF
LTADR=3630	/LT HNDLR ADDR IN DF
EXADR=3430	/EXT HNDLR ADDR IN DF
SDADR=2630	/SD HNDLR LOC IN DF
HDLRSZ=200-30	/HANDLER SIZE
DF=2000		/DATA FIELD START
IOBLK1=322	/FIRST IO ROUTINE BLOCK
IOBLK2=323	/SECOND IO ROUTINE BLOCK
IOCTBK=345	/IO CONTROLLER BLOCK
IOMBLK=365	/FIRST MASTER IO ROUTINE
		/BLOCK (CONTAINS LT)
/
/QANDA LOCATIONS
QAINIT=1000
QARFSH=1053
/CODE BEGIN
	LMODE
	LDF 1		/SWITCH TO DF 1
			/FOR STRINGS BU
/TELL USER WHAT IS GOING ON AND CONFIRM
/THAT THEY WISH TO CONTINUE
	JMP YESNO	/ASK THE USER
	DF SCONF
	AZE		/NO?
	SKP
	JMP HALT	/HALT/RESTART
/ASK THE USER IF THEY WANT INSTALL THE
/SUPER EXTENDED UNITS PATCH
	JMP YESNO	/ASK THEM
	DF SEXTU
	STC INEXTU	/SAVE AS EXT FG
			/BIT0=1 IF YES
/ASK THE USER IF THEY WANT TO HAVE DIAL
/USE SERIALDISK AS THE SYS DEVICE
	JMP YESNO	/ASK THE USER
	DF SASSYS
	ROR I 1		/ROT BOT IN LINK
	LDA I		/LOAD LT ADDR
	7630
	LZE		/ADD SD DIFF
			/IF SD IS SYS
	ADD KEXDIF	/-200
	STC HSYSAD	/SAVE SYS ADDR
	LDA I		/LOAD BASE BIN
	0370		/WORK AREA OFFS
	LZE		/MOVE TO 2ND SEC
	ADD K1000	/IF SD SYS
	STC WKBOF	/SAVE IT
/ASK THE USER IF THEY WANT TO COPY
/SYSTEM DATA TO FIRST SERIALDISK
/IFF WE ARE USING SD AS SYS
	LZE		/LINK UNTOUCHED
			/FROM EARLIER
	SKP
	JMP NOSCP	/NO? SKP THIS
	JMP YESNO	/ASK EM
	DF SCPSYS
	STC CSYSF	/SAVE TO FLAG
/READ TWO BLOCKS INTO DATA FIELD
NOSCP,	STD		/WAIT FOR TAPE
			/DONE IF RESTART
	JMP .-1
	LDF 3		/SWITCH MISC DF
	LDA I		/FORCE IO PRESET
	0020
	ESF
	RDC		/READ CONT BLK
	6000 IOCTBK
	RDC		/BLK HNDLR BLK
	7000 IOMBLK
/GO DOWN THE SUPER EXTENDED UNIT PATH
/IF REQUESTED
	SET I 10	/SET TBL ADDR
	MTADR-1
	LDA I
INEXTU,	0
	AZE		/SKIP IN NOT
	JMP EXTBL
/WRITE LINCTAPE UNIT ENTRIES
	CLR
	STC 13		/0 OFFSET INC
	STC 14		/AND UNIT NUM
	SET I 11	/SET HNDLR ADDR
	7630
	SET I 15	/SET COUNTER
	-10
	JMP WRENTS	/WRITE ENTRIES
/WRITE SPECIAL/SYSTEM UNITS
	LDA I	/SYSTEM AREA
	100		/UNIT NUMBER
	SET 11		/HNDLR ADDR
	HSYSAD		/SET EARLIER
	SET I 12	/BLK OFFSET
	300
	JMP WRENT	/WRITE SYS ENTRY
	LDA I		/SOURCE WORK
	110
	SET I 12	/NEW BLK OFFSET
	370
	JMP WRENT	/WRITE SRC WORK
	LDA I
	111
	SET 12		/OFFS MAYBE DIFF
	WKBOF
	JMP WRENT	/WRITE BIN WORK
/WRITE SERIALDISK ENTRIES
	SET I 14	/SET START UNIT
	10
	SET I 11	/SET EXT HNDLR
	7430		/ADDRESS
	SET I 13	/SET OFFS INC
	400
	SET I 15	/SET COUNT
	-6
	JMP WRENTS	/WRITE ENTRIES
/WRITE SECOND SD ENTRIES
/	SET I 14	/START UNIT
/	1010
/	JMP WRENTS	/WRITE EM
/WRITE ACTUAL HANDLER DATA
WRHNDR,			/WRITE HANDLER
			/SUPER EXT PATH
			/RETS HERE ALSO
	SET I 1		/SET SRC LOC
	SDADR-1		/SD HNDLR IN IF
	SET I 2		/SET DST LOC
	EXADR-1		/EXT HNDLR IN DF
	SET I 3		/SET COUNTER
	-HDLRSZ
WREXLP,	LDA I 1		/LOAD SRC WORD
	STA I 2		/STORE TO DST
	XSK I 3		/INC COUNTER
	JMP WREXLP	/NOT DONE? LOOP
			/DONE? CONTINUE
/WRITE BLOCKS BACK TO TAPE
	WRC		/WRITE 232 BACK
	6000 IOBLK1
	WRC		/WRITE 233 BACK
	7000 IOBLK2		
/COPY SYSTEM AREA OVER IF REQUESTED
/THIS USES THE DIAL HANDLERS WE ALREADY
/HAVE IN CORE
	SRO I		/B11 WILL BE SET
			/IF YES
CSYSF,	0000		/COPY SYS FLAG
	SKP
	JMP NOCSY	/NO? WE ARE DONE
			/YES? CONTINUE
	LDA I		/SETUP BLK NUMS
	300		/SYS START
	STC DRDAR+2	/SAVE READ BLK
	STC DWTAR+2	/SAVE WRITE BLK
	ADD DRDAR+3	/ALSO COUNT FOR
	STC DWTAR+3	/WRITE
	SET I 1		/SETUP COUNTER
	-70		/70 BLOCKS
	/COPY LOOP
CSYLP,	LDA I		/CHECK IF WE
	20		/NEED TO CUT
			/WRITE LENGTH
	ADM
	0001
	APO		/IF YES, ADD
			/AN OFFSET
	CLR		/NO->NO OFFSET
	COM
	ADD DWTAR+3
	STC DWTAR+3
			/(OVER READING
			/IS FINE)
	JMP DREAD	/READ BLOCKS
	JMP DWRITE	/WRITE EM BACK
	LDA I		/INC BLK NUMS
	20
	ADM		/READ NUM
	DRDAR+2
	LDA I
	20
	ADM		/WRITE NUM
	DWTAR+2
	LDA		/CHECK COUNTER
K1,	0001
	APO		/ARE WE DONE?
	JMP CSYLP	/NO? LOOP BACK
/WRITE GARBAGE TO THE INDEX TO DESTROY
/IT. THE DATA WE WRITE DOES NOT MATTER
/SO LONG AS IT DOES NOT BEGIN WITH "/"
/DIAL WILL CLEANUP FOR US LATER
	LDA I		/SET BLK NUM
	46		/TO 1ST INDEX
	STC DWTAR+2
	ADD K1		/COUNT TO 1
	STC DWTAR+3
	LDF 4		/0 1ST WORD
	STA
	2000
	LDF 3		/BACK HOME
	JMP DWRITE	/WRITE IT
/TELL USER WE ARE DONE
/AND MAYBE RETURN TO DIAL
NOCSY,	LDF 1		/TO STR LAND
	JMP YESNO
	DF SDONE
	AZE
	JMP DRET	/YES? JMP DIAL
/SPOOL TAPE TO BEGINNING AND RESTART
	LDF 3		/BACK TO TAPE
			/DATA LAND
	XOA		/SET NO-PAUSE
	BSE I
	0010
	AXO
	RDE
	6000
	JMP 20		/RESTART
/
/SUPER EXTENDED UNIT TABLE PATCH PATH
/WILL RETURN TO MAIN POST-TABLE WRITING
EXTBL,	SET I 11	/SETUP LT ENTRY
	7770		/MASK
	SET I 12
	0000		/VALUE
	SET I 13
	7630		/HNDLR
	SET I 14
	0000		/OFFSET
	JMP WEXENT	/WRITE IT
	/WRITE SYS ENTRIES
	SET I 11	/SYS AREA
	7777		/UNIT 100
	SET I 12
	-0100+1		/1S COMP STUFF
	SET 13
	HSYSAD		/EX SYS HNDLR
	SET I 14
	300
	JMP WEXENT	/WRITE IT
	SET I 11	/BIN WORK AREA
	7777		/UNIT 111
	SET I 12
	-0111+1
	SET 14
	WKBOF
	JMP WEXENT	/WRITE IT
	XSK I 12	/INC UNIT 110
	SET I 14	/FIXED OFFSET
	0370
	JMP WEXENT	/WRITE IT
	/WRITE SERIAL DISK ENTRIES
	SET I 11	/MASK
	7707
	SET I 12	/BITMASK VAL
	0
	SET I 2		/COUNTER
	-6
	SET I 13	/HNDLR ADDR
	7430
	SET I 14	/CLEAR OFFSET
	0
EXLP,	JMP WEXENT	/WRITE AN ENTRY
	LDA I		/INC OFFSET
	400
	ADM
	0014
	CLR
	LDA I		/GET 7777(2S -1)
	7777
	LAM		/ADD TO BM VAL
	0012
	XSK I 2		/INC COUNTER
	JMP EXLP	/NOT DONE? LOOP
			/DONE? CONTINUE
	/WRITE EXT UNIT TABLE PATCH
	SET I 10	/PATCH ADDR
	/USPAT-1 DF	/ASSY IS BEING
			/DUMB WITH THIS
			/SO HARDCODE IT
	2000
	JMP WRPAT	/WRITE PATCH
	JMP WRHNDR	/RETURN TO MAIN
/
/WRITE A PATCH CONSISTING OF ALTERNATING
/LOCATION AND DATA PAIRS
/AN ADDRESS OF ZERO WILL BE INTERPRETED
/AS THE END OF THE PATCH
/PARAM[10] PATCH LOCATION-1
WRPAT,	SET 12	/SAVE RET
	0
WRPLP,	LDA I 10	/LOAD ADDR
	AZE		/0 ADDR=DONE
	SKP
	JMP 12
	STC 11		/SAVE ADDR
	LDA I 10	/LOAD DATA WORD
	STA 11		/SAVE TO ADDR
	JMP WRPLP	/LOOP BACK
/WRITE A STANDARD UNIT TABLE ENTRY
/THIS WILL WRITE A UNIT ENTRY TO A UNIT
/TABLE USING PROVIDED VALUES
/PARAM[AC] UNIT NUMBER
/PARAM[10] NEW ENTRY ADDR-1
/PARAM[11] HANDLER ADDRESS
/PARAM[12] BLOCK OFFSET
/AC WILL BE CLEARED
/10 WILL CONTAIN THE ENTRY END ADDR
WRENT,	STA I 10	/SAVE UNIT
	LDA		/GET HNDLR ADDR
	0011
	STA I 10	/SAVE IT
	LDA		/GET BLK OFFSET
	0012
	STA I 10	/SAVE IT
	JMP 0		/RETURN
/
/WRITE STANDARD ENTRY SET
/THIS WILL WRITE A SET OF UNIT ENTRIES
/TO A UNIT TABLE
/BLOCK OFFSET WILL START AT 0, BUT CAN
/BE BE INCREASED BY A FIXED AMOUNT AFTER
/EACH ADDED ENTRY
/PARAM[10] UNIT TABLE ADDRESS-1
/PARAM[11] HANDLER ADDRESS
/PARAM[13] OFFSET INCREMENT
/PARAM[14] FIRST UNIT NUMBER
/PARAM[15] NEGATED ENTRY COUNT
WRENTS,	SET 17		/SAVE RET
	0
	SET I 12	/SET START OFFS
	0
WRSLP,	LDA		/GET UNIT NUM
	0014
	JMP WRENT	/WRITE ENTRY
	LDA 		/LOAD ADDR INC
	0013
	ADM		/ADD IT ON
	0012
	XSK I 14	/INC UNIT
	XSK I 15	/INC COUNTER
	JMP WRSLP	/NOT DONE? LOOP
	JMP 17		/DONE? RET
/
/WRITE SUPER EXTENDED UNIT TABLE ENTRY
/PARAM[10] NEW ENTRY LOCATION-1
/PARAM[11] BITMASK VALUE
/PARAM[12] BITMASK RESULT VALUE
/PARAM[13] HANDLER ADDRESS
/PARAM[14] BLOCK OFFSET
WEXENT,	LDA 		/GET MASK
	0011
	STA I 10
	LDA 		/GET VALUE
	0012
	STA I 10
	LDA		/GET HNDLR
	0013
	STA I 10
	LDA 		/GET BLK OFFS
	0014
	STA I 10
	JMP 0		/RET
/
/
/HALT ROUTINE
/CAUSE A HALT THEN RESTART IF THE USER
/CONTINUES AFTER
HALT,	HLT
	JMP 20
/ABORT WITH A MESSAGE
/MESSAGE ADDRESS SHOULD BE STORED IN
ABMSG,	LDF 1		/TO STR DF
	JMP YESNO
	SABRT
	AZE		/YES? RESTART
	JMP HALT	/NO? HALT
	JMP 20		/RESTART
/ASK A YES OR NO QUESTION AND RETURN
/THE RESULT IN AC
/IF YES, AC=1, OTHERWISE AC=0
/MESSAGE ADDRESS SHOULD BE STORED IN
/THE LOCATION FOLLOWING THE JMP TO HERE
YNBUF,	0
	0
YESNO,	LDA
	0
	BCL I		/CLEAR JMP BITS
	JMP
	STC 11		/SAVE TO INDEX
	LDA 11		/GET STR ADDR
	STC YNSTR	/SAVE IT
	SET 10	/SAVE RET ADDR
	0
	JMP QAINIT
YNSTR,	0000
	YNBUF
	JMP QARFSH
	LDA		/LOAD ANSWER
	YNBUF
	SAE I		/IS MARK+Y?
	7431
	CLR		/NO? CLEAR
	XSK I 10	/INC RET
	NOP		/GUARD
	JMP 10
/
/GLOBAL VARS
HSYSAD,	0		/SYS HNDLR ADR
WKBOF,	0		/BIN WORK OFFS
/
/CONSTANTS
KEXDIF,	-200	/DIFF BETWEEN LT AND EXT
K1000,	1000
		/UNIT HANDLER ADDRESSES
	PAGE
/
/PDP STUFF
/
/CONSTANTS
KREAD,	7774
KWRITE,	7775
KRESET,	7777
/
/DIAL-MS IO ROUTINE ARGS
/BLOCK START NUMBER CHANGES
/OTHER ARGS NEVER CHANGE
/
/READ ARGS
DRDAR,	0		/UNIT 0
	0020		/FIELD 1 START
	0		/1ST BLK NUM
	20		/BLK COUNT
/
/WRITE ARGS
DWTAR,	100		/SYS AREA UNIT
	0020		/FIELD 1 START
	0		/1ST BLK NUM
	20		/BLK COUNT
/
/IO ROUTINE CALLERS
/
/READ ROUTINE
DREAD,	PDP		/TO 8MODE
	PMODE
	JMS I KREAD
	DRDAR
	LINC		/BACK TO LMODE
	LMODE
	JMP 0		/RETURN
/
/WRITE ROUTINE
DWRITE,	PDP		/TO 8MODE
	PMODE
	JMS I KWRITE
	DWTAR
	LINC		/TO TO L
	LMODE
	JMP 0		/RETURN
/
/RETURN TO DIAL
DRET,	PDP		/TO 8MODE
	PMODE
	JMP I KRESET
	HLT		/SHOULD NOT RET
	LMODE
/
/
/MISC DATA SEGMENT
/TAPE DATA LIVES IN SECOND HALF
SEGMNT 3
/
/SUPER EXTENDED UNIT SELECTOR PATCHES
/DIAL-MS USES A UNIT TABLE TO SELECT
/APPROPRIATE HANDLERS AND OFFSETS FOR
/EACH UNIT. ORIGINALLY, THIS TABLE
/INCLUDED AN ENTRY FOR EACH UNIT,
/DESPITE MOST UNITS SHARING THE SAME 
/ATTRIBS. THESE PATCHES MODIFY THE UNIT
/TABLE STRUCTURE TO INCLUDE A BITMASK +
/EXPECTED RESULT TO MATCH UNIT INFO.
/THE BITMASK IS APPLIED TO UNIT BEING
/SEARCHED. IF THE RESULT MATCHES THE 
/RESULT IN THE ENTRY, THAT ENTRY IS
/SELECTED. THIS SAVES A LOT OF SPACE.
/ADDRESS; PATCH
	0		/THINGS GET ODD
			/IF WE START AT 			/ADDR 0
USPAT,	DF 1030; 4330	/JMS STCDF
	DF 1031; 5242	/JMP 42
	DF 1043; 1742	/TAD I TEMP
	DF 1044; 7450	/SNA
	DF 1045; 5257	/JMP MNOT
	DF 1046; 0317	/AND CUINT
	DF 1047; 2342	/ISZ TEMP
	DF 1050; 1742	/TAD I TEMP
	DF 1056; 5243	/JMP MLOOP
	0		/TERMINATOR
/
/
/STRING SEGMENT
SEGMNT 1
/QA STRINGS
/CONFIRMATION PROMPT
	0	/QANDA DOESNT LIKE STRS
		/TO START AT LOC 0 IN DF
		/SO ADD ONE PADDING WORD
SCONF,	TEXT "FSERIAL DISK INSTALLER
 
THIS WILL INSTALL THE SERIAL DISK HANDLER
HFOR LAP6-DIAL-MS. IF ANOTHER EXTRA DEVICE
HANDLER IS INSTALLED, IT WILL BE REPLACED
WITH THE SERIAL DISK HANDLER.THIS WILL
CONFIGURE THE UNIT NUMBERS AS FOLLOWS:
* 0-7: LINCTAPE
* 10-17: SERIAL DISK 1 HALF 1
MAKE SURE YOUR LAP6-DIAL-MS TAPE IS MOUNTED
ON UNIT 0 AND THAT WRITE ENABLE IS SET.
CONTINUE? (Y/N) <1\"
/
/INSTALL AS SYSTEM PROMPT
SASSYS,	TEXT "FSERIAL DISK INSTALLER
 
SETUP SERIAL DISK AS SYSTEM DEVICE.
THIS WILL PUT THE SOURCE WORK AND SYSTEM
AREAS ON THE FIRST PART OF THE SERIAL
DISK AND THE BINARY WORK AREA ON THE SECOND
PART. BOOTING AND USING LAP6-DIAL-MS WILL
REQUIRE A SERIAL DISK CONNECTION.
DO YOU WANT TO SETUP THE SERIAL DISK AS 
THE SYSTEM/WORK DEVICE? <1\"
/
/INSTALL FINISHED MESSAGE
SDONE,	TEXT "FSERIAL DISK INSTALLER
 
THE SERIAL DISK HANDLER WAS INSTALLED.
WOULD YOU LIKE TO RETURN TO DIAL-MS?
(Y/N) <1\"
/
/SUPER EXTENDED UNIT MODE PROMPT
SEXTU,	TEXT "FSERIAL DISK INSATLLER
 
INSTALL SUPER EXTENDED UNIT PATCH
THIS WILL PATCH THE DIAL-MS UNIT SELECTION
LOGIC TO ALLOW FULL USE OF ALL 4 POSSIBLE
SERIAL DISKS, BUT MAY CHANGE SOME BEHAVIOR.
THE FOLLOWING ADDITIONAL UNITS WILL BE ADDED:
* 20-27: SERIAL DISK 1 HALF 2
* 30-37: SERIAL DISK 2 HALF 1
* 40-47: SERIAL DISK 3 HALF 2
INSTALL THE PATCH? (Y/N) <1\"
/
/COPY SYSTEM TO SERIALDISK PROMPT
SCPSYS,	TEXT "FSERIALDISK INSTALLER
 
WOULD YOU LIKE TO COPY THE SYSTEM AREA
HFROM LINCTAPE UNIT 0 TO THE FIRST
SERIAL DISK UNIT? (Y/N) <1\"
/
/ABORT/NONSTANDARD INSTALL MESSAGE
SABRT,	TEXT "FSERIAL DISK INSTALLER
 
DETECTED MODIFIED/CORRUPT I/O ROUTINE DATA!
CANNOT CONTINUE!
RESTART? (Y/N) <1\"
